    module Test;
    import Subprocess;
    
    test : IO ();
    test = (
        println("Run \"ls -l -r\".");;
        let (_, exit_status) = *run_with_stream("ls", ["ls", "-l", "-r"], |(stdin, stdout, stderr)| (
            let output = *read_string(stdout); // Read standard output of the command.
            println(output).lift
        )).try(exit_with_msg(1));
        assert_eq(|_|"", exit_status.as_exit, 0_U8);;
    
        println("Run \"sed s/w/W/\" and write \"Hello world!\" to the standard input.");;
        let (_, exit_status) = *run_with_stream("sed", ["/usr/bin/sed", "s/w/W/"], |(stdin, stdout, stderr)| (
            write_string(stdin, "Hello world!");;
            close_file(stdin).lift;; // Send EOF.
            let output = *read_string(stdout); // Read standard output of the command.
            println(output).lift
        )).try(exit_with_msg(1));
        assert_eq(|_|"", exit_status.as_exit, 0_U8);;
     
        pure()
    );